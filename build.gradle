plugins {
	id 'java'
	id 'antlr'
	id 'groovy'
}

group = 'com.virgo47'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	jcenter()
}

ext {
	testNgVersion = '6.10'
	assertjVersion = '3.8.0'
	antlrVersion = '4.7'
	spockVersion = '1.1-groovy-2.4'
	groovyVersion = '2.4.12'
}

sourceSets.main.antlr.srcDirs = ['src/main/antlr4']

dependencies {
	antlr "org.antlr:antlr4:${antlrVersion}"

	testCompile "org.testng:testng:$testNgVersion"
	testCompile "org.assertj:assertj-core:$assertjVersion"
	testCompile "org.spockframework:spock-core:$spockVersion"
	testCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
}

generateGrammarSource {
	arguments += [
		// Package declaration in generated sources can be specified also with @header in a grammar
		// file, but this then creates the package line twice in Maven build.
		"-package", "com.virgo47.vexpressed.grammar",
		"-visitor",
		"-long-messages"
	]
}

compileJava {
	options.compilerArgs << '-Xlint:unchecked'
}

compileTestJava {
	options.compilerArgs << '-Xlint:unchecked'
}

// obsolete, will migrate to Spock specs
task testng(type: Test) {
	useTestNG()
}

test {
	dependsOn testng
	// for Spock
	useJUnit()
}